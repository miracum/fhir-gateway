plugins {
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.spotless' version '8.2.1'
    id 'net.ltgt.errorprone' version '5.0.0'
}

group = 'org.miracum.etl'
version = '5.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', '2025.1.1')
    set('hapiVersion', '8.6.5')
}

dependencies {
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:${hapiVersion}"
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'
    implementation 'io.micrometer:micrometer-core:1.16.3'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.3'
    implementation 'org.miracum:kafka-fhir-serializer:3.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.retry:spring-retry'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2:2.4.240'
    runtimeOnly 'org.postgresql:postgresql:42.7.9'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    errorprone 'com.google.errorprone:error_prone_core:2.47.0'
    errorprone 'com.uber.nullaway:nullaway:0.13.1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        csv.required = true
        html.required = true
        xml.required = true
    }
}

jar {
    enabled = false
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

import net.ltgt.gradle.errorprone.CheckSeverity

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        check('NullAway', CheckSeverity.ERROR)
        option('NullAway:AnnotatedPackages', 'org.miracum')
    }
    // Include to disable NullAway on test code
    if (name.toLowerCase().contains('test')) {
        options.errorprone {
            disable('NullAway')
        }
    }
}
